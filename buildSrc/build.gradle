plugins {
    id "groovy"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7"
}

apply from: "../dependencies.gradle"

repositories {
    jcenter()
    google()
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs = ["src/main/java", "src/main/groovy"]

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile deps.build.androidPlugin
    compile deps.build.commonsIo
    compile deps.build.commonsLang
    compile deps.build.kotlinPlugin
    compile deps.build.mavenArtifact

    testCompile deps.test.junit
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

def publishVersion = '0.20.10'
group = 'com.uber'
version = publishVersion
def siteUrl = 'https://github.com/uber/okbuck'
def gitUrl = 'https://github.com/uber/okbuck.git'

def getReleaseRepositoryUrl() {
  return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
      : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getRepositoryUsername() {
  return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
  return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            artifactId 'okbuck'
        }
    }
    repositories {
      maven {
        name 'release'
        url getReleaseRepositoryUrl()
        credentials {
          username getRepositoryUsername()
          password getRepositoryPassword()
        }
      }
    }
}

bintray {
    user = project.properties.get("bintray.user")
    key = project.properties.get("bintray.apikey")
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'gradle-plugins'
        name = 'okbuck'
        userOrg = 'uber'
        desc = 'Gradle plugin that lets developers utilize the Buck build system on a Gradle project'
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['buck', 'okbuck', 'uber']
        publicDownloadNumbers = true
        version {
            name = publishVersion
            released = new java.text.SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date())
            vcsTag = publishVersion
        }
    }
}
